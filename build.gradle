plugins {
    id 'jacoco'
    id 'java'
    id 'groovy'
    id 'me.champeau.jmh' version '0.7.2'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'com.montplex'
version = '1.0.0'

repositories {
    maven {
        url 'https://maven.aliyun.com/repository/public'
    }
    mavenCentral()
}

sourceSets {
    main {
        java {
//            srcDirs = ['src/main/java', 'build/generated/source/proto/main/java']
            srcDirs = ['src/main/java']
        }
        groovy {
            srcDirs = ['src/main/groovy', 'dyn/src', 'dyn/ctrl']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
        proto {
            srcDirs = ['src/main/proto']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        groovy {
            srcDirs = ['src/test/groovy', 'dyn/test']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
        proto {
            srcDirs = ['src/test/proto']
        }
    }
}

sourceCompatibility = 21
targetCompatibility = 21

compileGroovy {
    groovyOptions.configurationScript = file("config.groovy")
}

def activejVersion = '6.0-beta2'

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.9'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.20.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.20.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j2-impl', version: '2.20.0'

    implementation group: 'org.apache.groovy', name: 'groovy', version: '4.0.12'
    implementation group: 'org.apache.groovy', name: 'groovy-dateutil', version: '4.0.12'

    implementation group: 'io.activej', name: 'activej-types', version: activejVersion
    implementation group: 'io.activej', name: 'activej-common', version: activejVersion
    implementation group: 'io.activej', name: 'activej-bytebuf', version: activejVersion
    implementation group: 'io.activej', name: 'activej-datastream', version: activejVersion
    implementation group: 'io.activej', name: 'activej-csp', version: activejVersion
    implementation group: 'io.activej', name: 'activej-eventloop', version: activejVersion
    implementation group: 'io.activej', name: 'activej-net', version: activejVersion
    implementation group: 'io.activej', name: 'activej-http', version: activejVersion
    implementation group: 'io.activej', name: 'activej-inject', version: activejVersion
    implementation group: 'io.activej', name: 'activej-launcher', version: activejVersion
    implementation group: 'io.activej', name: 'activej-launchers-common', version: activejVersion
    implementation group: 'io.activej', name: 'activej-servicegraph', version: activejVersion
    implementation group: 'io.activej', name: 'activej-config', version: activejVersion
    implementation group: 'io.activej', name: 'activej-boot', version: activejVersion

    implementation group: 'commons-io', name: 'commons-io', version: '2.15.1'
    implementation group: 'commons-net', name: 'commons-net', version: '3.9.0'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.17.1'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    implementation group: 'org.apache.commons', name: 'commons-compress', version: '1.26.0'
    implementation group: 'io.netty', name: 'netty-buffer', version: '4.1.100.Final'

    implementation group: 'com.google.guava', name: 'guava', version: '32.1.3-jre'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '4.29.2'

    implementation group: 'com.github.oshi', name: 'oshi-core', version: '6.7.1'

    // for hyperloglog
    implementation group: 'com.github.prasanthj', name: 'hyperloglog', version: '1.1'
    implementation group: 'it.unimi.dsi', name: 'fastutil', version: '8.5.15'
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.9.0'

    implementation group: 'com.github.jnr', name: 'jnr-posix', version: '3.1.19'
    implementation group: 'com.github.jnr', name: 'jnr-ffi', version: '2.2.16'
    implementation group: 'net.openhft', name: 'affinity', version: '3.23.3'
    implementation group: 'net.openhft', name: 'posix', version: '2.25ea0'

    implementation group: 'com.github.luben', name: 'zstd-jni', version: '1.5.5-10'
    implementation group: 'org.lz4', name: 'lz4-java', version: '1.8.0'

    implementation group: 'com.moilioncircle', name: 'redis-replicator', version: '3.8.1'

    implementation group: 'redis.clients', name: 'jedis', version: '4.4.7'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.14.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.14.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.3'

    implementation group: 'io.prometheus', name: 'simpleclient', version: '0.16.0'
    implementation group: 'io.prometheus', name: 'simpleclient_httpserver', version: '0.16.0'
    implementation group: 'io.prometheus', name: 'simpleclient_hotspot', version: '0.16.0'

    implementation group: 'com.github.oshi', name: 'oshi-core', version: '6.8.2'

    implementation group: 'org.apache.lucene', name: 'lucene-core', version: '9.11.1'

    implementation group: 'org.rocksdb', name: 'rocksdbjni', version: '9.7.3'
    implementation group: 'org.apache.parquet', name: 'parquet-common', version: '1.15.0'
    implementation group: 'org.apache.parquet', name: 'parquet-column', version: '1.15.0'
    implementation group: 'org.apache.parquet', name: 'parquet-hadoop', version: '1.15.0'
    implementation group: 'org.apache.hadoop', name: 'hadoop-client', version: '3.4.1'

    implementation group: 'org.apache.curator', name: 'curator-framework', version: '5.7.0'
    implementation group: 'org.apache.curator', name: 'curator-recipes', version: '5.7.0'
    implementation group: 'org.apache.curator', name: 'curator-client', version: '5.7.0'

    implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.49.v20220914'
    implementation group: 'com.github.kevinsawicki', name: 'http-request', version: '6.0'

//    implementation group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.36'
//    implementation group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: '1.36'

    jmh 'org.openjdk.jmh:jmh-core:1.36'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.36'

    implementation project(':segment_common')
    implementation project(':segmentweb')

    testImplementation group: 'org.openjdk.jol', name: 'jol-core', version: '0.17'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-4.0'
}

jmh {
    iterations = 1
    fork = 1
    jmhVersion = '1.36'
    zip64 = true

    // change here to run target jmh tests
//    includes = ['OneSlotRead*']
    includes = ['SlotCalc*']
}

protobuf {
    protoc {
        // download from repositories
        artifact = 'com.google.protobuf:protoc:4.29.2'
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }

        ofSourceSet('main')
    }
}

test {
    useJUnitPlatform()

    maxHeapSize = '8G'
    // parallel = true
    forkEvery = 1

    testLogging {
        showStandardStreams = true
    }
}

jacoco {
    toolVersion = '0.8.12'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('reports/jacocoHtml')
    }
}

test.finalizedBy jacocoTestReport

// for debug
/*
java \
-Xmx1g -Xms1g -XX:+UseZGC -XX:+ZGenerational -XX:MaxDirectMemorySize=64m \
-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=*:17379 \
-jar velo-1.0.0.jar
 */

jar {
    manifest {
        attributes "Main-Class": 'io.velo.MultiWorkerServer'
        attributes "Class-Path": '. lib/' + configurations.runtimeClasspath.collect { it.name }.join(' lib/')
    }
    exclude 'log4j2.xml', 'velo.properties', 'conf.properties'
    zip64 true
}

task fatJar(type: Jar) {
    manifest {
        attributes "Main-Class": 'io.velo.MultiWorkerServer'
        attributes "Class-Path": '.'
    }
    archiveBaseName = 'velo-all-in-one'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude '*.dll'
    }
    with jar
    zip64 true
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task testJar(type: Jar) {
    archiveClassifier.set('tests')
    from sourceSets.test.output
    include '**/*.class'
}

task copyTestDependencies(type: Copy) {
    from configurations.testRuntimeClasspath
    into 'build/libs/lib'
}

testJar.dependsOn copyTestDependencies

task copyDynScript(type: Copy) {
    from './dyn/src'
    into "$buildDir/libs/dyn/src"
}

task copyLog4j2(type: Copy) {
    from 'src/main/resources/log4j2.xml'
    into "$buildDir/libs/"
}

task copyConfigFile(type: Copy) {
    from 'src/main/resources/velo.properties', 'src/main/resources/velo_extend.properties', 'src/main/resources/conf.properties'
    into "$buildDir/libs/"
}

task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into 'build/libs/lib'
}

task cBuildLzf(type: Exec) {
    commandLine 'bash', '-c', 'cd src/main/c/redis_lzf && mkdir -p build && cd build && cmake .. -B . && make'
}

def javaHome = System.getenv("JAVA_HOME")

task cBuildCrc64(type: Exec) {
    commandLine 'bash', '-c', 'cd src/main/c/redis_crc64 && mkdir -p build && cd build && cmake .. -B . && make'
    // env
    environment 'JAVA_HOME', javaHome
}

task cLibCopy(type: Copy) {
    from 'src/main/c/redis_lzf/build/libredis_lzf.so', 'src/main/c/redis_crc64/build/libredis_crc64.so'
    into 'build/libs/lib'
}

cLibCopy.dependsOn cBuildLzf, cBuildCrc64

jar.dependsOn copyDynScript, copyLog4j2, copyConfigFile, copyDependencies, cLibCopy